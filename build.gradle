plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'me.archdev'
version = '0.3.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    // Spigot API - Provided by the server
    compileOnly "org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT"

    // Java Discord API (JDA)
    implementation "net.dv8tion:JDA:5.5.1"

    // Lombok
    compileOnly "org.projectlombok:lombok:1.18.24"
    annotationProcessor "org.projectlombok:lombok:1.18.24"
    testCompileOnly "org.projectlombok:lombok:1.18.24"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.24"

    // HikariCP
    implementation 'com.zaxxer:HikariCP:3.4.5'

    // H2 Database
    implementation 'com.h2database:h2:1.4.200'

    // MySQL Connector
    implementation 'mysql:mysql-connector-java:8.0.21'

    // SQLite JDBC
    implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'net.dv8tion', 'me.archdev.staffrelay.jda'
}

tasks.build.dependsOn shadowJar